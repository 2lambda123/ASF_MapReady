/*************************************************************************
 * Copyright(c) 1996, California Institute of Technology.                *
 *             ALL RIGHTS RESERVED.                                      *
 * U.S. Government Sponsorship acknowledged.                             *
 *************************************************************************/

/*=======================
	@(#)sp_check_pending.crt	1.1  11/21/96
=========================*/

if exists (select * from sysobjects where name = "sp_check_pending")
	drop proc sp_check_pending
go

create proc sp_check_pending ( @job_id int, @job_type JobTypeType)
as

	--  return code :
	--  0 : normal
	--  1 : invalid job_type
	--  2 : invalid job_id		

	declare @gha_angle	real,
		@tce_rev	int,
		@sv_rev		int,
		@platform	SatType,
		@scan_file	varchar(255),
		@params_file	varchar(255),
		@gha_avail	char(3),
		@tce_avail	char(3),
		@sv_avail	char(3),
		@scan_avail	char(3),
		@params_avail	char(3),
		@job_exists	tinyint	

	select @gha_avail = "NO", @tce_avail = "NO", @sv_avail = "NO",
		@scan_avail = "NO", @params_avail = "NO"	

	if @job_type = "L1"
	begin
		select 	@gha_angle = gha_angle,
			@tce_rev = tce_rev,
			@sv_rev = sv_rev,
			@scan_file = scan_results_file,
			@params_file = cal_params_file,
			@platform = platform
		from L1_procparms
		where job_id = @job_id	

		select @job_exists =  @@rowcount
	end

	else if @job_type = "SCAN"
	begin
                select  @gha_angle = gha_angle,
                        @tce_rev = tce_rev,     
                        @sv_rev = sv_rev,      
                        @scan_file = "",
                        @params_file = "" ,
			@platform = platform
		from scan_orders
		where job_id = @job_id

		select @job_exists = @@rowcount
	end

	else
	begin
		print "Invalid job_type : %1!", @job_type
		return 1
	end

	if @job_exists = 0
	begin
		print "Unable to find %1! job with job_id %2!", @job_type, @job_id
		return 2
	end
 
	if @gha_angle is not null
		select @gha_avail = "YES" 

	if @platform = "R1"
		select @tce_avail = "N/A"
	else if @tce_rev is not null
		select @tce_avail = "YES" 

	if @sv_rev is not null
		select @sv_avail = "YES" 

	if @job_type = "SCAN"
		select @scan_avail = "N/A"
	else if (@scan_file <> "" and @scan_file is not null)
		select @scan_avail = "YES" 

	if @job_type = "SCAN"
		select @params_avail = "N/A"
	else if (@params_file <> "" and @params_file is not null)
		select @params_avail = "YES" 


	print "   ======================== Availability ======================="
	print " "
	print " "

	select "GHA  "  = @gha_avail,
		"TCE " = @tce_avail,
		"State Vector " = @sv_avail,
		"Scan Results File " = @scan_avail,
		"Cal Params File " = @params_avail
		
return 0 
go
