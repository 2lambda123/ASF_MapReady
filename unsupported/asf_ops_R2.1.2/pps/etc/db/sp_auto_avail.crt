/*************************************************************************
 * Copyright(c) 1996, California Institute of Technology.                *
 *             ALL RIGHTS RESERVED.                                      *
 * U.S. Government Sponsorship acknowledged.                             *
 *************************************************************************/

/*=======================
	@(#)sp_auto_avail.crt	1.3  12/16/96
=========================*/

if exists (select * from sysobjects where name = "sp_auto_avail")
	drop proc sp_auto_avail
go

create proc sp_auto_avail ( @job_id int ) 
as
	declare @priority  char(15)
	declare @auto_flag char(15)
	declare @job_type  char(15)
	declare @job_comment  varchar(255)
	declare @insert_top_flag  LogicalType
	declare @quicklook_flag  LogicalType
	declare @max_seq   int
	declare @ret       int

	select  @max_seq   = 0
	select  @ret       = 0

	select  @job_type = job_type,
		@priority = priority,
		@job_comment = job_comment,
		@quicklook_flag = quicklook_flag,
		@insert_top_flag = insert_top_flag
		from jobs
		where jobs.job_id = @job_id 

        /* turn insert_top_flag on for QLK job */
        if (@quicklook_flag = "YES")
	        select @insert_top_flag = "YES"

	select @auto_flag = auto_flag
		from policy
		where policy.job_type = @job_type 
		and   policy.job_priority = @priority
		and   policy.quicklook_flag = @quicklook_flag

	if @auto_flag = 'YES'
	begin


		/* change state to AVAILABLE */
		update jobs
			set job_state = 'AVAILABLE',
			    insert_top_flag = @insert_top_flag
			where job_id = @job_id 

		if (@@transtate > 1)
		begin
			return -100	
		end

		/* insert into schedule table */
		exec @ret = sp_insert_schedule 
			@job_id,@job_type,@job_comment,
			@quicklook_flag,@priority,@insert_top_flag

                if (@ret != 0) 
                begin
                        return -200
                end

		select @ret = 1

	end


	/* ret < 0 fail : Unable to make job available for processing */
	/* ret = 0  Success: auto_flag not set or job is not ready */
	/* ret = 1  Success: Job is available for processing */

	return @ret
go
