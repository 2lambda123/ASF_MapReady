C Alaska SAR Processor (ASP) %W% %E% %U%
      SUBROUTINE RDMTRIC (ISTAT,SRADV,SVLV,EULANG,CA,RZERO,
     *  DELTAR,CFDOT,GAINTBL,NTHETA,IFIRST,DTHETA,REQ,RFLAT,
     *  NPTS,MCV,SCLMCV,X)
c/*   SUBROUTINE RDMTRIC (ISTAT,SRADV,SVLV,EULANG,CA,RZERO, ---------
c    *  DELTAR,CFDOT,GAINTBL,NTHETA,IFIRST,DTHETA,REQ,RFLAT,
c    *  NPTS,MCV,SCLMCV,X)
c
C     COMPUTES THE "RANGE RADIOMETRIC CORRECTION" FOR THE ASP 
C     (INTEGER*2 ARRAY MCV)
C     SUBROUTINE ARGUMENTS ARE OF TYPES REAL AND INTEGER EXCEPT 
C     WHEN SPECIFICALLY STATED OTHERWISE.
C     SUBROUTINE IS ALSO CONTROLLED BY THE OPTIONS L1,L2,... OF 
C     LABELLED COMMON/LOPT/ (SEE SUBROUTINE OPT1).
C
C     OPTIONS:
C     ********
C
C     L1     SELECTS REPRESENTATION OF OUTPUT ARRAY MCV
C     L1=1   2'S-COMPLEMENT WITH MSB REPRESENTING -(2**0).
C     L1=2   UNSIGNED MAGNITUDE WITH MSB REPRESENTING 2**(-1)
C
C     L2     CONTROLS SCREEN OUTPUT
C     L2=1   NO SCREEN OUTPUT
C     L2=2   OUTPUTS TO THE SCREEN FOR EACH RANGE THE FOLLOWING
C            VARIABLES IN SEQUENCE:
C                      SLANT RANGE
C                      ANGULAR DEVIATION FROM BORE-SIGHT
C                      INCIDENCE ANGLE
C                      FLOATING-POINT VERSION OF MCV     
C
C     L3     SELECTS COMPUTATION OF CIRCLE-ELLIPSOID INTERSECTION.
C     L3=1   EXACT COMPUTATION 
C     L3=2   CIRCULAR ARC APPROXIMATED BY PARABOLIC ARC.
C
C     L4     CONTROLS SELECTION OF EARTH ELLIPSOID PARAMETERS
C     L4=1   PARAMETERS INTERNALLY SET: 
C                    EQUATORIAL RADIUS = 6378.140E3 METERS.
C                    RECIPROCAL FLATTENING FACTOR = 298.257
C            (INPUTS REQ,RFLAT IGNORED)
C     L4=2   INPUT PARAMETERS REQ,RFLAT ADOPTED.
C
C     INPUT ARGUMENTS:
C     ****************
C
C     SRADV    RADIUS-VECTOR TO SATELLITE IN "QUASI-INERTIAL, 
C              ARIES TRUE-OF-DATE" EQUATORIAL FRAME (PATH FIG. 4. 
C              PATH VARIABLES NUMBERED 26-28).
C              DOUBLE PRECISION   
C     SVLV     VELOCITY-VECTOR SATELLITE IN "QUASI-INERTIAL, 
C              ARIES TRUE-OF-DATE" EQUATORIAL FRAME (PATH FIG. 4. 
C              PATH VARIABLES NUMBERED 29-31).
C              DOUBLE PRECISION   
C     EULANG   3 EULER ANGLES CONTROLLING ROTATIONS DETERMINING 
C              INSTANTANEOUS ORIENTATION OF THE RIGHT-HANDED 
C              SATELLITE-FIXED FRAME (G1,G2,G3).
C              SEQUENCE OF ROTATIONS AND ORDER OF ANGLES IN ARRAY 
C              IS:
C                 ROLL, YAW, PITCH.
C                 (SEE SUBROUTINE DEULER2)
C     CA       THE ANTENNA BORE-SIGHT VECTOR LIES IN THE G2,G3 
C              PLANE. CA IS THE ANGLE IT MAKES WITH (-G3).
C     RZERO    SMALLEST SLANT RANGE.
C              DOUBLE PRECISION   
C     DELTAR   SLANT-RANGE STEP.
C              DOUBLE PRECISION   
C     CFDOT    COEFFICIENTS OF FDOT QUADRATIC (0:2)
C              CFDOT(0)=CONSTANT TERM
C     GAINTBL  ELEVATION ANTENNA-GAIN TABLE AS A FUNCTION OF 
C              ANGULAR DEVIATION FROM BORE-SIGHT.
C     NTHETA   NUMBER OF ENTRIES IN ANTENNA-GAIN TABLE (1:NTHETA)
C     IFIRST   GAINTBL(1) IS THE GAIN FOR ANGLE IFIRST*DTHETA
C     DTHETA   ANGLE STEP IN ANTENNA-GAIN TABLE
C     REQ      EARTH EQUATORIAL RADIUS (BUT SEE OPTION L4).
C     RFLAT    RECIPROCAL OF EARTH FLATTENING FACTOR (BUT SEE 
C              OPTION L4)
C     NPTS     NUMBER OF POINTS IN OUTPUT ARRAY MCV (0:(NPTS-1))
C
C
C     OUTPUT PARAMETERS:
C     ****************** 
C   
C     ISTAT    STATUS FLAG
C                 ISTAT = 0:  NORMAL RETURN (MCV VALID).
C                 ISTAT = 1:  NO EARTH POINT SATISFIES INPUT 
C                             PARAMETERS.
C                 ISTAT = 2:  DEVIATION FROM BORE-SIGHT FALLS 
C                             OUTSIDE ANTENNA-GAIN TABLE.
C     MCV      INTEGER*2 ARRAY OF NORMALIZED "RANGE RADIOMETRIC 
C              CORRECTION" FACTORS. MCV SHOULD BE DIMENSIONED
C              (0:(NPTS-1)) BY CALLING PROGRAM. SEE OPTION L1.
C     SCLMCV   THE "TRUE" (UNNORMALIZED) FACTOR IS GIVEN BY 
C              SCLMCV*MCV(I)
C     X        ARRAY FOR INTERMEDIATE COMPUTATIONS.
C              THIS IS THE REAL EQUIVALENT OF ARRAY MCV.
C              SHOULD BE DIMENSIONED (0:(NPTS-1)) BY CALLING 
C              PROGRAM.    
C   
C     LABELLED  COMMON/CRDMTRIC/ PROVIDES DATA FOR THESE ERROR 
C     EXITS
C
C     ALL ANGLES ARE IN DEGREES.
C     ALL LENGTHS ARE IN METERS
C*/
      INTEGER*2 MCV(0:0)
      REAL CFDOT(0:2),GAINTBL(1),EULANG(3),X(0:0)
      DOUBLE PRECISION G1(3),G2(3),G3(3),DEULANG(3),SANGI,D
      DOUBLE PRECISION RE,REDAT,RCF,RCFDAT,SRADV(3),SVLV(3)
      DOUBLE PRECISION PHI1,PHI2,DM1(3),DM2(3),RHO(3),RHON(3)
      DOUBLE PRECISION SLANTR,RZERO,DELTAR,DI,RHOANT(3),R3(3)
      DOUBLE PRECISION CARAD,RHOCRCL(3),F1(3),F2(3),F3(3),AX(3)
      DOUBLE PRECISION SRADMAG,SVMAG,SINPSI
      COMMON/CDFRAME11/SRADMAG,SVMAG,SINPSI
      COMMON/CDTSCT2/PHI1,PHI2
      COMMON/CRDMTRIC/I,THETA
      COMMON/LOPT/L1,L2,L3,L4,L5,L6,L7,L8,L9,L10,L11,L12,L13,L14
      DATA DGPRAD/57.29577951/
      DATA RADPDG/1.745329252E-2/
      DATA REDAT/6378.140D3/ ! IAU (1976) SYSTEM
      DATA RCFDAT/298.257D0/ ! IAU (1976) SYSTEM
      ISTAT=0
      NM1=NPTS-1
      DO 30 I=1,3
   30 DEULANG(I)=DBLE(EULANG(I))
      GO TO (31,32),L4
   31 RE=REDAT
      RCF=RCFDAT
      GO TO 33
   32 RE=DBLE(REQ)
      RCF=DBLE(RFLAT)
   33 AX(1)=RE
      AX(2)=RE
      AX(3)=RE*(1.D0-1.D0/RCF)
c  call debug4 (11,4,2,AX(1),AX(3))
      CALL DFRAME11 (SRADV,SVLV,F1,F2,F3)
c  CALL DEBUG4 (12,4,3,SRADMAG,SVMAG,SINPSI)
c  call debug4 (112,4,3,F1(1),F1(2),F1(3))
c  call debug4 (212,4,3,F2(1),F2(2),F2(3))
c  call debug4 (312,4,3,F3(1),F3(2),F3(3))
      CARAD=DBLE(RADPDG*CA)
      CALL DEULER2 (DEULANG,F1,F2,F3,G1,G2,G3)
c  call debug4 (113,4,3,G1(1),G1(2),G1(3))
c  call debug4 (213,4,3,G2(1),G2(2),G2(3))
c  call debug4 (313,4,3,G3(1),G3(2),G3(3))
      CALL DSUMV2 (RHOANT,-DSIN(CARAD),G2,-DCOS(CARAD),G3,3)
c         call debug4 (14,4,3,RHOANT(1),RHOANT(2),RHOANT(3))
      CALL DSCALE1 (RHOCRCL,-1.D0,G1,3)
      DO 8 I=0,NM1
      SI=FLOAT(I)
      DI=DBLE(SI)
      FDOT=ABS(((CFDOT(2)*SI)+CFDOT(1))*SI+CFDOT(0))
      SLANTR=RZERO+DELTAR*DI
      CALL DSCALE1 (DM1,SLANTR,RHOANT,3)
c    call debug4 (16,2,1,I)
      GO TO (4,5),L3
    4 CALL DITSCT2 (AX,SRADV,RHOCRCL,DM1,R3,DM2,N)
      GO TO 7
    5 CALL DITSCT3 (AX,SRADV,RHOCRCL,DM1,R3,DM2,N)
    7 IF (N.NE.3) GO TO 20
      ISTAT=1
      RETURN
   20 THETA=SNGL(PHI1)
C     PHI1 IS OBTAINED FROM LABELLED COMMON/CDTSCT2/
c     call debug4 (17,4,4,r3(1),r3(2),r3(3),slantr)
c      write(6,*)theta,dtheta,ntheta,ifirst
      CALL INTRP81 (*3,GAIN,THETA/DTHETA,GAINTBL,NTHETA,IFIRST)
      CALL DELPSD1 (AX,R3,RHON)
c     call debug4 (18,3,4,theta,dtheta,gain,fdot)
c    call debug4 (19,4,3,rhon(1),rhon(2),rhon(3))
      CALL DSUMV (DM1,SRADV,R3,3,1)
      CALL DVNR1 (DM1,3,RHO,D)
      CALL DVECTP (RHO,RHON,DM1)
      CALL DVNR1 (DM1,3,DM2,SANGI)
      RSANGI=SNGL(SANGI)
      RSLANTR=SNGL(SLANTR)
      X(I)=SQRT(FDOT*RSANGI)*(RSLANTR**2)*(SQRT(GAIN))
      != REAL FORM OF MCV
		! GAIN is inverse of antenna gain
      GO TO (8,9),L2
    9 ANGI=DGPRAD*ASIN(RSANGI) ! = INCIDENCE ANGLE
      CALL DEBUG4 (1,3,4,RSLANTR,THETA,ANGI,X(I))
    8 CONTINUE

      GO TO (15,13),L1
   13 CALL CNVRT13 (X,MCV,NM1,SCLMCV)
      RETURN
   15 CALL CNVRT15 (X,MCV,NM1,SCLMCV)
      RETURN
    3 ISTAT=2
      RETURN
      END

      SUBROUTINE RDMINIT(I1,I2,I3,I4)
C/*   SUBROUTINE RDMINIT(I1,I2,I3,I4) ------------------------
C
C     Sets the four options L1 thru L4 in labelled common LOPT
C*/
      COMMON/LOPT/L1,L2,L3,L4,L5,L6,L7,L8,L9,L10,L11,L12,L13,L14
      L1 = I1
      L2 = I2
      L3 = I3
      L4 = I4
      RETURN
      END
