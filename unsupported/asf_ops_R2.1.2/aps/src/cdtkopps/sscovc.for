C--  Copyright (c)1996, California Institute of Technology.
C--  U.S. Government Sponsorship acknowledged.

*************************************************************************
*  Name:	SSCOVC
*  Module Type: SUBROUTINE	Language: FORTRAN
*  $Logfile:   ACS003:[BLD.MPS.LIB.SRC]SSCOVC.FOV  $
*  Purpose:	GIVEN A CIRCLE SITE AND A SET OF SWATH POINTS, COMPUTE
*		THE SWATH POINTS WHERE COVERAGE BEGINS AND ENDS.  
*  Subroutines called:
*  GCBOMB, GCTRVF, SWENTC
*  MUST LINK VECTOR LIBRARY, DOUBLE PRECISION.
*  Input Parameters:
*  Name         Type    Definition
*  CENTER(3)	REAL*8	XYZ OF CENTER OF CIRCLE
*  RADIUS	REAL*8	RADIUS OF THE CIRCLE
*  POINT(10,*)	REAL*8	SWATH POINTS:  
*			POINT(1,J) =  EPHEMERIS TIME OF POINT J
*			POINT(2,J) =  GROUND TRACK X AT TIME J
*			POINT(3,J) =  GROUND TRACK Y AT TIME J
*			POINT(4,J) =  GROUND TRACK Z AT TIME J
*			POINT(5,J) =  SWATH LEFT POINT X AT TIME J
*			POINT(6,J) =  SWATH LEFT POINT Y AT TIME J
*			POINT(7,J) =  SWATH LEFT POINT Z AT TIME J
*			POINT(8,J) =  SWATH RIGHT POINT X AT TIME J
*			POINT(9,J) =  SWATH RIGHT POINT Y AT TIME J
*			POINT(10,J) = SWATH RIGHT POINT Z AT TIME J
*		THE LEFT AND RIGHT ORIENTAION IS AS IF YOU ARE IN THE MIDDLE
*		OF THE SWATH AND FACING IN THE DIRECTION OF MOTION.  
*  NPOINT	INTEGER	NUMBER OF POINTS IN ARRAY POINT
*  Output Parameters:
*  Name         Type    Definition
*  START(10)	REAL*8	INFORMATION AT START OF SWATH COVERAGE OF CIRCLE
*			SAME STRUCTURE AS IN POINTS.  
*			IF NO COVERAGE FOR SITE, ALL VALUES ARE 0.0D00 
*  STOP(10)	REAL*8	INFORMATION AT END OF SWATH COVERAGE.
*			SAME STRUCTURE AS IN POINTS.  
*			IF NO COVERAGE FOR SITE, ALL VALUES ARE 0.0D00 
*  ASCDSC	CH*1	A     IF ASCENDING
*			D     IF DESCENDING
*			BLANK IF, DURING COVERAGE, THE SATELLITE GOES FROM
*			      ONE TO THE OTHER.
*  Variables:
*  Locals :
*  Externals :
*  Modification History:                                            
*  Date			Revision	Author
*  $Date$ $Revision$ $Author$
*                                                                   
*********************************************************************/
      SUBROUTINE SSCOVC (CENTER,RADIUS,POINT,NPOINT,START,STOP,ASCDSC)
      character*100 SccsFileID
     -/'@(#)sscovc.for	5.1 98/01/08 APS/ASF\0'/

      IMPLICIT NONE
      REAL*8 POINT(10,*), CENTER(3), RADIUS, START(10), STOP(10)
      CHARACTER*(*) ASCDSC
      CHARACTER*1 ASCDSC1, ASCDSC2
      INTEGER NPOINT
      INTEGER J, K, L
      REAL*8 DIF, FRAC, PI8
      INCLUDE 'APS_HOME:include/local/mps_const_math.inc'
C---	PI IN REAL*8 REPRESENTATION:
      PI8 = PI
      IF(RADIUS.GE.PI8/2.0D00) CALL GCBOMB
      IF(NPOINT.LE.1) CALL GCBOMB
      IF(NPOINT.GT.99) CALL GCBOMB
      DO 1000 J = 1,10
      START (J) = 0
      STOP (J) = 0
 1000 CONTINUE
      ASCDSC = ' '
      ASCDSC1 = ' '
      ASCDSC2 = ' '
C---	GO THROUGH THE LIST TO GET BEGINNING OF SWATH COVERAGE:
CCCC      WRITE(*,*) 'SSCOVC:  BEGINNING OF SWATH COV'
      DO 2000 J = 1,NPOINT-1
C---	SWATH ENTERS THE CIRCLE:
      K = J + 1
      CALL SWENTC(
C---                  SW LEFT 1  SW RT 1    SW LEFT 2  SW RT 2
     ?  CENTER,RADIUS,POINT(5,J),POINT(8,J),POINT(5,K),POINT(8,K),FRAC)
      IF(FRAC.LT.0.0D00) GO TO 2000 
C---	OK.  FRAC IS .GE. 0 SO THAT SWATH COVERAGE STARTED ON THIS LEG 
C---	OF COVERAGE.  FRAC IS THE FRACTIONAL MOVEMENT FROM POINT J TO POINT
C---	J+1 WHEN COVERAGE STARTED.  WE USE THIS TO INTERPOLATE AND GET THE TIME
C---	OF THE START OF THE COVERAGE AND THE THREE POINTS AT THAT TIME.  
C--	THEY GO INTO THE START ARRAY.  
C---	1	TIME
      DIF = POINT(1,K) - POINT(1,J)
      START(1) = POINT(1,J) + (DIF * FRAC)
C---	2,3,4 GROUND XYZ
      CALL GCTRVF(START(2),POINT(2,J),POINT(2,K),FRAC)
C---	NOTE IF ASCENDING OR DESCENDING GROUND TRACK AT START:
      CALL SSCVAD(START(2),POINT(2,J),POINT(2,K),ASCDSC1)
C---	5,6,7 LEFT SWATH POINT XYZ
      CALL GCTRVF(START(5),POINT(5,J),POINT(5,K),FRAC)
C---	8,9,10 RIGHT SWATH POINT XYZ
      CALL GCTRVF(START(8),POINT(8,J),POINT(8,K),FRAC)
      GO TO 3000
 2000 CONTINUE
C---	NO COVERAGE BECAUSE NO ENTRY DETECTED.  
      GO TO 9999
 3000 CONTINUE
CCCC      WRITE(*,*) 'SSCOVC:  END OF SWATH COV'
C---	GO THROUGH THE LIST TO GET END OF SWATH COVERAGE:
C---	WE WILL USE THE SWATH ENTRY ROUTINE, BUT CALL IT WITH THE POINTS 
C---	REVERSED, I.E., IN REVERSE CHRONOLOGICAL ORDER.  
C---	ALSO, WE WILL GO THROUGH THE LIST OF POINTS IN REVERSE CHRONOLOGICAL 
C---	ORDER.  
C---	AND SINCE WE ARE FACING THE OTHER DIRECTION, LEFT AND RIGHT ARE 
C---	SWITCHED.  IT HELPS TO DRAW A PICTURE.  
C---	WE WILL START WITH J = NPOINT AND K = NPOINT - 1
C---    THE CODE WILL LOOK THE SAME AS ABOVE, EXCEPT FOR THE LEFT-RIGHT SWITCH
C---	AND THE FACT THAT THE RESULTS GO INTO THE START ARRAY.  
      DO 4000 L = 1,NPOINT-1
      J = NPOINT - L + 1
      K = J - 1
      CALL SWENTC(
     ? CENTER,RADIUS,POINT(8,J),POINT(5,J),POINT(8,K),POINT(5,K),FRAC)
      IF(FRAC.LT.0.0D00) GO TO 4000 
      DIF = POINT(1,K) - POINT(1,J)
      STOP(1) = POINT(1,J) + (DIF * FRAC)
      CALL GCTRVF(STOP(2),POINT(2,J),POINT(2,K),FRAC)
C---	NOTE IF ASCENDING OR DESCENDING GROUND TRACK AT STOP:
C---	NOTE THAT THE CALL TO SSCVAD MUST BE FROM PT K TO PT J IN 
C---	CHRONOLOGICAL ORDER.  ( K < J )
      CALL SSCVAD(STOP(2),POINT(2,K),POINT(2,J),ASCDSC2)
      CALL GCTRVF(STOP(5),POINT(5,J),POINT(5,K),FRAC)
      CALL GCTRVF(STOP(8),POINT(8,J),POINT(8,K),FRAC)
C---	OK. DONE.  
      IF(ASCDSC1 .EQ. ASCDSC2) ASCDSC = ASCDSC1
      GO TO 9999
 4000 CONTINUE
C---	ERROR.  WE FOUND A BEGIN COVERAGE, BUT NO END.  
      CALL GCBOMB
 9999 CONTINUE
      RETURN
      END
